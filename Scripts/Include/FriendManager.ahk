;===============================================================================
; FriendManager.ahk - Friend Management Functions
;===============================================================================
; This file contains functions for managing in-game friends.
; These functions handle:
;   - Adding friends by friend code
;   - Removing all friends
;   - Getting friend code from account
;   - Showcase likes
;   - Trade tutorial handling
;   - Friend input field management
;
; Dependencies: ADB.ahk, Utils.ahk (for ReadFile), image recognition
; Used by: Main bot loop for friend management and trading setup
;===============================================================================

; Windows clipboard is global, lock only around clear/click/read (per attempt, ~0.5s).
AccountFriendInfo_AcquireClipboardLock(timeoutMs := 800) {
    lockName := "Global\PTCGPB_FriendCodeClipboard"
    hMutex := DllCall("CreateMutex", "Ptr", 0, "Int", false, "Str", lockName, "Ptr")
    if (!hMutex)
        return 0

    waitResult := DllCall("WaitForSingleObject", "Ptr", hMutex, "UInt", timeoutMs, "UInt")
    if (waitResult != 0 && waitResult != 0x80) {
        DllCall("CloseHandle", "Ptr", hMutex)
        return 0
    }
    return hMutex
}

AccountFriendInfo_ReleaseClipboardLock(hMutex) {
    if (!hMutex)
        return
    DllCall("ReleaseMutex", "Ptr", hMutex)
    DllCall("CloseHandle", "Ptr", hMutex)
}

TryDismissSocialFirstTutorial(failSafeTime := 0) {
    if (failSafeTime < 5 || Mod(failSafeTime, 2) != 0)
        return false

    adbClick_wbb(145, 451)
    Delay(0.2)
    adbClick_wbb(167, 447)
    Delay(0.2)
    adbClick_wbb(38, 460)
    Delay(0.2)
    adbClick_wbb(155, 425)
    Delay(0.2)
    return true
}

TryHandleTradeTutorial(failSafeTime := 0) {
    if (failSafeTime < 6 || Mod(failSafeTime, 2) != 0)
        return false

    if (FindOrLoseImage("Friend_AddButtonInFriendList", 0, , , true))
        return false

    adbClick_wbb(167, 447)
    Delay(0.3)
    adbClick_wbb(38, 460)
    Delay(1)
    adbClick_wbb(38, 460)
    Delay(0.3)

    return true
}

;-------------------------------------------------------------------------------
; 1 Pack Method: skip friend renew after Immersive / Crown / Shiny pack
;-------------------------------------------------------------------------------
PackMethod_UpdateSkipFriendRenewFromCounts(foundImmersive, foundCrown, foundShiny1Star, foundShiny2Star) {
    global session

    if (!session.get("packMethod"))
        return

    if (foundImmersive || foundCrown || foundShiny1Star || foundShiny2Star)
        session.set("packMethodSkipFriendRenew", 1)
    else
        session.set("packMethodSkipFriendRenew", 0)
}

PackMethod_RenewFriends() {
    global session

    session.set("packMethodStayOnPackScreen", 0)
    if (session.get("packMethodSkipFriendRenew")) {
        session.set("packMethodSkipFriendRenew", 0)
        session.set("packMethodStayOnPackScreen", 1)
        LogInfo("1 Pack Method: skipping friend renew (Immersive/Crown/Shiny in previous pack)")
        return session.get("friendsAdded")
    }
    return AddFriends(true)
}

; After PackOpening we are usually on the pack select screen. Skip GoToMain/SelectPack when renew was skipped.
PackMethod_ConsumeStayOnPackScreen() {
    global session

    if (!session.get("packMethodStayOnPackScreen"))
        return false
    session.set("packMethodStayOnPackScreen", 0)
    LogInfo("1 Pack Method: already on pack screen, skipping GoToMain/SelectPack")
    return true
}

;-------------------------------------------------------------------------------
; UniqueArray - Return a copy of the array with only the first occurrence of
; each value preserved (preserves original order).
;-------------------------------------------------------------------------------
UniqueArray(arr) {
    seen := {}
    result := []
    for _, value in arr {
        if (!seen[value]) {
            seen[value] := true
            result.Push(value)
        }
    }
    return result
}

;-------------------------------------------------------------------------------
; SelectGroupRerollFriendIDs - Pick 9 random IDs from ids.txt plus the configured
; FriendID for group reroll. If ids.txt has fewer than 10 IDs, use all of them.
; The returned list never contains duplicates, even if FriendID is also in ids.txt.
;-------------------------------------------------------------------------------
SelectGroupRerollFriendIDs(fileIDs, friendID) {
    if (!IsObject(fileIDs))
        fileIDs := []

    fileIDs := UniqueArray(fileIDs)

    n := fileIDs.MaxIndex()
    if (!n)
        n := 0

    ; Not enough IDs for the 10-ID sampling rule: fall back to using all IDs.
    if (n < 10) {
        if (friendID != "" && !HasVal(fileIDs, friendID))
            fileIDs.Push(friendID)
        return fileIDs
    }

    ; Build a pool of ids.txt entries excluding the FriendID.
    pool := []
    for _, id in fileIDs {
        if (id != friendID)
            pool.Push(id)
    }

    if (!pool.MaxIndex() || pool.MaxIndex() < 9) {
        if (friendID != "" && !HasVal(fileIDs, friendID))
            fileIDs.Push(friendID)
        return fileIDs
    }

    ; Shuffle pool (Fisher-Yates) and take the first 9.
    poolN := pool.MaxIndex()
    Loop % poolN {
        i := poolN - A_Index + 1
        Random, j, 1, %i%
        temp := pool[i] . ""
        pool[i] := pool[j] . ""
        pool[j] := temp . ""
    }

    selected := []
    Loop 9 {
        selected.Push(pool[A_Index])
    }

    ; The 10th slot is always the configured FriendID.
    if (friendID != "")
        selected.Push(friendID)

    return selected
}

;-------------------------------------------------------------------------------
; AddFriends - Add friends from friend code list
;-------------------------------------------------------------------------------
AddFriends(renew := false, getFC := false) {
    prof := Prof_Scope(A_ThisFunc)
    global botConfig, session, interceptProc

    writeLastActivityEpoch(session.get("scriptName"), 4000)

    ; Friend adding is Inject Wonderpick-only; own friend-code lookup is allowed wherever the caller explicitly asks for it.
    if (!getFC && botConfig.get("deleteMethod") != "Inject Wonderpick 96P+") {
        clearLastActivityEpoch(session.get("scriptName"))
        return false
    }

    if (!getFC && (botConfig.get("groupRerollEnabled") || botConfig.get("useSoloIdsFile"))) {
        friendIDs := ReadFile("ids")
        if (!friendIDs)
            friendIDs := []
        if (botConfig.get("groupRerollEnabled")) {
            ; Group reroll: pick 9 random IDs from ids.txt plus the FriendID.
            friendIDs := SelectGroupRerollFriendIDs(friendIDs, botConfig.get("FriendID"))
        } else if(!HasVal(friendIDs, botConfig.get("FriendID")) && botConfig.get("FriendID") != "") {
            ; Solo ids file: keep using the full list, appending the FriendID if missing.
            friendIDs.Push(botConfig.get("FriendID"))
        }
        session.set("friendIDs", friendIDs)
    } else if (!getFC) {
        session.set("friendIDs", false)
    }
    friendIDsAvailable := IsObject(session.get("friendIDs")) && session.get("friendIDs").MaxIndex()
    if(!getFC && !friendIDsAvailable && botConfig.get("FriendID") = "") {
        clearLastActivityEpoch(session.get("scriptName"))
        return false
    }

    session.set("failSafe", A_TickCount)
    failSafeTime := 0
    Loop {
        if (DismissFriendFlowBlockingPopup("Waiting for Social"))
            continue

        if (IsSocialTabActiveOnHub(failSafeTime))
            break

        adbClick_wbb(143, 518)
        if(IsSocialTabActiveOnHub(failSafeTime)) {
            break
        }
        else if(FindOrLoseImage("Common_PopupXButtonInMain", 0, , , true)){
            adbClick_wbb(137, 480)
            Delay(1)
        }
        else if(TryHandleTradeTutorial(failSafeTime))
            continue
        else if(TryDismissSocialFirstTutorial(failSafeTime))
            continue
        else if(!renew && !getFC) {
            Delay(3)
            if (!ShouldSkipGenericButtonInSocialWait()) {
                clickButton := FindOrLoseImage("Common_ColorChangeButton", 0, , 80)
                if(clickButton) {
                    StringSplit, pos, clickButton, `,  ; Split at ", "
                    adbClick_wbb(pos1, pos2)
                }
            }
        }
        else if(FindOrLoseImage("Create_TutorialUseResourceForOpenPack", 0)) {
            Delay(3)
            adbClick_wbb(146, 441) ; 146 440
            Delay(3)
            adbClick_wbb(146, 441)
            Delay(3)
            adbClick_wbb(146, 441)
            Delay(3)

            FindImageAndClick("Create_TutorialPremiumPass", 168, 438, , 500, 5) ;stop at hourglasses tutorial 2
            Delay(1)

            adbClick_wbb(203, 436) ; 203 436
        }
        failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
        if (getFC)
            CreateStatusMessage("Retrieving account info`nOpening Social (" . failSafeTime . "/90 seconds)")
        else
            CreateStatusMessage("Waiting for Social`n(" . failSafeTime . "/90 seconds)")
        writeLastActivityEpoch(session.get("scriptName"), 4000)
    }

    GoToFriendsList(true, false)

    if(getFC)
        return AccountFriendInfo_CopyFriendCodeFromCurrentScreen()

    EnsureAccountFriendInfo("Inject Wonderpick 96P+", true)

    ; start adding friends
    if(!session.get("friendIDs")){
        session.set("friendIDs", [])
        session.get("friendIDs").Push(botConfig.get("FriendID"))  ; Use an array to hold the single friend ID
    }
    FindImageAndClick("Friend_SearchFriendWindowCancelButtonCorner", 75, 440)
    FindFriendIDInputAndClick("", "initial")

    ;randomize friend id list to not back up mains if running in groups since they'll be sent in a random order.
    n := session.get("friendIDs").MaxIndex()
    Loop % n
    {
        i := n - A_Index + 1
        Random, j, 1, %i%
        ; Force string assignment with quotes
        temp := session.get("friendIDs")[i] . ""  ; Concatenation ensures string type
        session.get("friendIDs")[i] := session.get("friendIDs")[j] . ""
        session.get("friendIDs")[j] := temp . ""
    }
    friendIDIdx := 1
    while(friendIDIdx <= session.get("friendIDs").maxIndex()){
        value := session.get("friendIDs")[friendIDIdx]

        if (StrLen(value) != 16) {
            ; Wrong id value
            friendIDIdx += 1
            continue
        }
        session.set("failSafe", A_TickCount)
        failSafeTime := 0
        skipCurrentID := false
        Loop {
            isContinue := false
            isSendReqeest := false
            if(!SubmitFriendIDSearch(value, friendIDIdx, n)) {
                skipCurrentID := true
                break
            }
            Delay(1)
            if(FindOrLoseImage("Friend_RequestButtonInSearchResult", 0, failSafeTime, 80)) {
                adbClick_wbb(243, 258)
                MarkFriendCleanupPending("Friend request submitted")
                Delay(1)
                gosub, WaitAfterFriendRequestSend
                break
            }
            else if(FindOrLoseImage("Friend_WithdrawButton", 0, failSafeTime)) {
                MarkFriendCleanupPending("Friend request pending")
                break
            }
            else if(FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0, failSafeTime)) {
                LogToFile("Friend details request button detected during AddFriends | index=" . friendIDIdx)
                adbClick_wbb(143, 407)
                MarkFriendCleanupPending("Friend request submitted from details")
                Delay(1)

                interceptProc := true
                waitSendResult := A_TickCount
                Loop{
                    Delay(0.25)
                    if(FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, failSafeTime)) {
                        MarkFriendCleanupPending("Friend accepted from details")
                        break
                    }
                    else if(interceptErrorCheck("ADD")){
                        skipCurrentID := true
                        LogToFile("Skipping friend ID after ADD error from details | index=" . friendIDIdx)
                        break
                    }
                    else if(FindOrLoseImage("Friend_CannotFriendRequest", 0, failSafeTime)) {
                        LogToFile("Skipping friend ID because cannot send friend request to this user from details | index=" . friendIDIdx)
                        break
                    }
                    if(!isSendReqeest
                        && (A_TickCount - waitSendResult) > 2500
                        && FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0, failSafeTime, , true)
                        && !FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, failSafeTime, , true)) {
                        adbClick_wbb(143, 407)
                        MarkFriendCleanupPending("Friend request resubmitted from details")
                        isSendReqeest := true
                    }
                    if ((A_TickCount - waitSendResult) > 10000)
                        break
                }
                interceptProc := false
                CloseFriendDetailsIfOpen()
                break
            }
            else if(FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, failSafeTime)) {
                MarkFriendCleanupPending("Friend accepted from details")
                CloseFriendDetailsIfOpen()
                break
            }
            else if(FindOrLoseImage("Friend_CannotFriendRequest", 0, failSafeTime)) {
                LogToFile("Skipping friend ID because cannot send friend request to this user | index=" . friendIDIdx)
                break
            }
            else if(interceptErrorCheck("ADD")) {
                ; LogToFile("Rate limit hit while adding friend; retrying same ID | index=" . friendIDIdx . " | id=" . value)
                isContinue := true
                break
            }
            else if(FindOrLoseImage("Friend_AcceptedButtonInSearchResult", 0, failSafeTime)) {
                MarkFriendCleanupPending("Friend accepted")
                if(renew){
                    interceptProc := true
                    FindImageAndClick("Friend_RemoveConfirmButtonInSearchResult", 193, 258)
                    if(interceptErrorCheck("ADD")) {
                        interceptProc := false
                        isContinue := true
                        break
                    }
                    FindImageAndClick("Friend_RequestButtonInSearchResult", 200, 372)
                    if(interceptErrorCheck("ADD")) {
                        interceptProc := false
                        isContinue := true
                        break
                    }
                    Delay(1) ; otherwise it will sometimes click before UI finishes loading
                    adbClick_wbb(243, 258)
                    MarkFriendCleanupPending("Friend request renewed")
                    gosub, WaitAfterFriendRequestSend
                }
                break
            }
            else
                adbInputEvent("59 122 67")

            if (!IsFriendSearchInputReady() && !IsFriendSearchDialogOpen())
                RecoverWrongScreenBeforeFriendIDInput("processing index=" . friendIDIdx)

            failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
            CreateStatusMessage("Processing add friends for `n(" . failSafeTime . "/45 seconds)")
            writeLastActivityEpoch(session.get("scriptName"), 4000)
        }

        if(skipCurrentID)
            LogDebug("Skipped friend ID during AddFriends | index=" . friendIDIdx)

        if(isContinue)
            continue

        if(friendIDIdx != session.get("friendIDs").maxIndex()) {
            if(interceptErrorCheck("ADD")) {
                isContinue := true
                continue
            }
            if (!FindFriendIDInputAndClick(1000, "next ID " . (friendIDIdx + 1) . "/" . n))
                break
            EraseInput(friendIDIdx, n)
        }
        friendIDIdx += 1
    }

    session.set("failSafe", A_TickCount)
    failSafeTime := 0
    Loop, {
        if (IsSocialTabActiveOnHub(failSafeTime))
            break
        adbClick_wbb(143, 518)
        Delay(3)
        if(IsSocialTabActiveOnHub(failSafeTime))
            break
        else if(FindOrLoseImage("Friend_SearchFriendWindowCancelButtonCorner", 0, failSafeTime))
            adbClick_wbb(80, 365)
    }

    ; ratelimit, only use this route when number of added ids is 6-10, 16-20, etc
    if (Mod(n - 1, 10) >= 10) {
        inventoryIconPos := FindImageAndClick("Menu_InventoryIconInMenu", 240, 494)
        DelayH(600)

        if(!inventoryIconPos) {
            restartGameInstance("Stuck at InSubMenu...")
            clearLastActivityEpoch(session.get("scriptName"))
            return false
        }

        menuRecoveryStart := A_TickCount
        Loop {
            if((A_TickCount - menuRecoveryStart) > 15000) {
                restartGameInstance("Stuck at InSubMenu...")
                clearLastActivityEpoch(session.get("scriptName"))
                return false
            }

            if(FindOrLoseImage("Create_DownloadAlertWindow", 0, , , true)) {
                adbClick_wbb(197, 365)
                Delay(1)
                if(FindOrLoseImage("Create_DownloadAlertWindow", 1))
                    break
                continue
            }

            if(FindOrLoseImage("Menu_GoToTitleButton_Down", 0, , 60, true)) {
                adbClick_wbb(137, 470)
                DelayH(300)
                continue
            }

            if(FindOrLoseImage("Menu_GoToTitleButton_Up", 0, , 60, true)) {
                adbClick_wbb(137, 430)
                DelayH(300)
                continue
            }

            currentInventoryIconPos := FindOrLoseImage("Menu_InventoryIconInMenu", 0, , 60, true)
            if(currentInventoryIconPos) {
                StringSplit, currentInventoryIconCoord, currentInventoryIconPos, `,
                currentMiscClickX := currentInventoryIconCoord1 + 8
                currentMiscClickY := currentInventoryIconCoord2 + 170
                adbClick_wbb(currentMiscClickX, currentMiscClickY)
                DelayH(300)
                continue
            }

            Delay(0.25)
        }
        session.set("isReloadAfterAddFriends", true)
    }
    else {
        Loop {
            if(FindOrLoseImage("Friend_BottomDarkHomeIcon", 0))
                break
            else{
                adbClick_wbb(40, 516)
                Delay(0.1)
                adbClick_wbb(175, 445)
                DelayH(500)
            }
        }
        ;FindImageAndClick("Friend_BottomDarkHomeIcon", 40, 516, , 500)

        Loop % botConfig.get("waitTime") {
            CreateStatusMessage("Waiting for friends to accept request`n(" . A_Index . "/" . botConfig.get("waitTime") . " seconds)")
            sleep, 1000
            writeLastActivityEpoch(session.get("scriptName"), 4000)
        }
    }
    clearLastActivityEpoch(session.get("scriptName"))
    return n ;return added friends so we can dynamically update the .txt in the middle of a run without leaving friends at the end

    WaitAfterFriendRequestSend:
    interceptProc := true
    waitSendResult := A_TickCount
    Loop{
        Delay(0.25)
        if(interceptErrorCheck("ADD")){
            isContinue := true
            break
        }
        if(FindOrLoseImage("Friend_WithdrawButton", 0, failSafeTime)) {
            MarkFriendCleanupPending("Friend request pending")
            break
        }
        else if(FindOrLoseImage("Friend_AcceptedButtonInSearchResult", 0, failSafeTime)) {
            MarkFriendCleanupPending("Friend accepted")
            break
        }
        else if(FindOrLoseImage("Friend_CannotFriendRequest", 0, failSafeTime)) {
            LogToFile("Skipping friend ID because cannot send friend request to this user | index=" . friendIDIdx)
            break
        }
        if(!isSendReqeest
            && (A_TickCount - waitSendResult) > 2500
            && FindOrLoseImage("Friend_RequestButtonInSearchResult", 0, failSafeTime, 40, true)
            && !FindOrLoseImage("Friend_WithdrawButton", 0, failSafeTime, , true)
            && !FindOrLoseImage("Friend_AcceptedButtonInSearchResult", 0, failSafeTime, , true)) {
            adbClick_wbb(243, 258)
            MarkFriendCleanupPending("Friend request resubmitted")
            isSendReqeest := true
        }
        if ((A_TickCount - waitSendResult) > 10000)
            break
    }
    if(interceptErrorCheck("ADD"))
        isContinue := true
    interceptProc := false
    return
}

;-------------------------------------------------------------------------------
; RemoveFriends - Remove all friends from account
;-------------------------------------------------------------------------------
RemoveFriends() {
    prof := Prof_Scope(A_ThisFunc)
    global botConfig, session, interceptProc, DeadCheck

    cleanupAccount := session.get("accountFileName")
    LogInfo("RemoveFriends start | account=" . cleanupAccount)
    LogDebug("RemoveFriends start detail | account=" . cleanupAccount . " | friended=" . session.get("friended") . " | DeadCheck=" . DeadCheck . " | deleteMethod=" . botConfig.get("deleteMethod") . " | useSoloIdsFile=" . botConfig.get("useSoloIdsFile"))
    writeLastActivityEpoch(session.get("scriptName"), 4000)

    ; Only allow RemoveFriends in Inject Wonderpick 96P+ mode
    if (botConfig.get("deleteMethod") != "Inject Wonderpick 96P+" && !botConfig.get("useSoloIdsFile")) {
        LogInfo("RemoveFriends skipped: unsupported delete method | account=" . cleanupAccount . " | deleteMethod=" . botConfig.get("deleteMethod") . " | useSoloIdsFile=" . botConfig.get("useSoloIdsFile"))
        DeadCheck := 0
        IniWrite, 0, % session.get("scriptIniFile"), UserSettings, DeadCheck
        ClearFriendCleanupPending()
        session.set("friended", false)
        clearLastActivityEpoch(session.get("scriptName"))
        return false
    }

    session.set("friendIDs", ReadFile("ids"))

    if(!session.get("friendIDs") && botConfig.get("FriendID") = "") {
        if (!session.get("friended") && DeadCheck != 1) {
            LogInfo("RemoveFriends skipped: no friend IDs and no cleanup pending | account=" . cleanupAccount . " | DeadCheck=" . DeadCheck)
            session.set("friended", false)
            clearLastActivityEpoch(session.get("scriptName"))
            return false
        }

        LogInfo("Friend IDs unavailable during RemoveFriends; continuing because cleanup is pending | account=" . cleanupAccount . " | DeadCheck=" . DeadCheck)
        LogInfo("Friend IDs unavailable during RemoveFriends; continuing because cleanup is pending | account=" . cleanupAccount . " | DeadCheck=" . DeadCheck, "GroupReroll.txt")
    }

    session.set("packsInPool", 0) ; if friends are removed, clear the pool

    CreateStatusMessage("Starting friend removal process...",,,, false)

    session.set("failSafe", A_TickCount)
    failSafeTime := 0
    Loop {
        if (DismissFriendFlowBlockingPopup("Waiting for Social"))
            continue

        if (IsSocialTabActiveOnHub(failSafeTime))
            break

        adbClick_wbb(143, 518)
        if(IsSocialTabActiveOnHub(failSafeTime))
            break
        else if(FindOrLoseImage("Common_PopupXButtonInMain", 0, , , true)){
            adbClick_wbb(137, 480)
            Delay(1)
        }
        else if(TryHandleTradeTutorial(failSafeTime))
            continue
        else if(TryDismissSocialFirstTutorial(failSafeTime))
            continue
        else if(FindOrLoseImage("Create_TutorialUseResourceForOpenPack", 0)) {
            Delay(3)
            adbClick_wbb(146, 441) ; 146 440
            Delay(3)
            adbClick_wbb(146, 441)
            Delay(3)
            adbClick_wbb(146, 441)
            Delay(3)

            FindImageAndClick("Create_TutorialPremiumPass", 168, 438, , 500, 5) ;stop at hourglasses tutorial 2
            Delay(1)

            adbClick_wbb(203, 436) ; 203 436
        } else if(!renew && !getFC && DeadCheck = 1) {
            if (!ShouldSkipGenericButtonInSocialWait()) {
                clickButton := FindOrLoseImage("Common_ColorChangeButton", 0, , 80)
                if(clickButton) {
                    StringSplit, pos, clickButton, `,  ; Split at ", "
                    adbClick_wbb(pos1, pos2)
                }
            }
        }
        Sleep, 500
        failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
        CreateStatusMessage("Waiting for Social`n(" . failSafeTime . "/90 seconds)")
        writeLastActivityEpoch(session.get("scriptName"), 4000)
    }

    GoToFriendsList(false, false)
    Delay(2)
    FindImageAndClick("Friend_FriendRequestsSubMenu", 167, 467, , 10)
    Delay(2)
    adbClick(167, 472) ; extra click since failing to get into requests sometimes
    session.set("failSafe", A_TickCount)
    failSafeTime := 0
    interceptProc := true
    Loop{
        if (DismissFriendFlowBlockingPopup("Waiting for clearAll"))
            continue

        if (FindOrLoseImage("Friend_ActivatedClearAllButton", 0))
            break
        if (FindOrLoseImage("Friend_DisabledDenyAllRequestButtonInApproveSubmenu", 0, , , true)) {
            LogInfo("No pending friend requests found during cleanup | account=" . cleanupAccount . " | DeadCheck=" . DeadCheck, "GroupReroll.txt")
            break
        }
        adbClick(205, 510)
        Delay(1)
        if (FindOrLoseImage("Friend_RemoveConfirmButtonInFriendDetails", 0))
            adbClick(210, 372)

        Delay(1)

        isErrorOccurred := interceptErrorCheck("CLEARALL")
        if(isErrorOccurred)
            continue

        failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
        if (failSafeTime >= 45) {
            LogInfo("Friend request cleanup timed out; continuing friend list cleanup | account=" . cleanupAccount . " | DeadCheck=" . DeadCheck, "GroupReroll.txt")
            break
        }
        CreateStatusMessage("Waiting for clearAll`n(" . failSafeTime . "/45 seconds)")
        writeLastActivityEpoch(session.get("scriptName"), 4000)
    }
    interceptProc := false
    FindImageAndClick("Friend_FriendListSubmenu", 22, 464, , 10)
    friendsProcessed := 0
    finished := false
    accepted := false
    Loop {
        session.set("failSafe", A_TickCount)
        failSafeTime := 0
        accepted := false
        Loop {
            if (DismissFriendFlowBlockingPopup("Waiting for friend details"))
                continue

            adbClick(58, 190)
            Delay(1)
            if(FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, failSafeTime)){
                accepted := true
                break
            }
            else if(FindOrLoseImage("Friend_FriendListSubmenu", 0, failSafeTime, 10)) {
                if(FindOrLoseImage("Friend_FriendListEmpty", 0, failSafeTime, 10)) {
                    finished := true
                    break
                }
            }
            else if(FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0, failSafeTime))
                break
            failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
            CreateStatusMessage("Waiting for Accepted2`n(" . failSafeTime . "/45 seconds)")
            writeLastActivityEpoch(session.get("scriptName"), 4000)
        }
        if(finished)
            break
        if(accepted){
            accepted := false

            FindImageAndClick("Friend_RemoveConfirmButtonInFriendDetails", 145, 407)

            interceptProc := true
            isContinue := false
            Loop, {
                if (DismissFriendFlowBlockingPopup("Confirming friend removal"))
                    continue

                adbClick(200, 372)
                Delay(0.5)
                if(FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0))
                    break
                else if(interceptErrorCheck("REMOVE")){
                    isContinue := true
                    break
                }
            }
            interceptProc := false
            if(isContinue)
                continue
        }
        session.set("failSafe", A_TickCount)
        failSafeTime := 0
        ; Either find "Add" (expected), or if we accidentally went back too many pages to "Social", go back into friends.
        Loop {
            if (DismissFriendFlowBlockingPopup("Returning to friend list"))
                continue

            adbClick(143, 507)
            Sleep, 750
            if(FindOrLoseImage("Common_ActivatedSocialInMainMenu", 0, failSafeTime)) {
                Sleep, 1000
                adbClick(38, 460)
                Sleep, 2000
                break
            }
            else if(FindOrLoseImage("Friend_AddButtonInFriendList", 0, failSafeTime))
                break
        }
        friendsProcessed++
        writeLastActivityEpoch(session.get("scriptName"), 4000)
    }

    ; Exit friend removal process
    CreateStatusMessage("Friend removal completed. Processed " . friendsProcessed . " friends. Returning to main...",,,, false)
    LogInfo("RemoveFriends complete | account=" . cleanupAccount . " | processed=" . friendsProcessed . " | DeadCheckBeforeClear=" . DeadCheck)
    clearLastActivityEpoch(session.get("scriptName"))
    DeadCheck := 0
    IniWrite, 0, % session.get("scriptIniFile"), UserSettings, DeadCheck
    ClearFriendCleanupPending()
    session.set("friended", false)
    LogDebug("RemoveFriends cleanup state cleared | account=" . cleanupAccount . " | processed=" . friendsProcessed)
    CreateStatusMessage("Friends removed successfully!",,,, false)

    if(session.get("stopToggle")) {
        CreateStatusMessage("Stopping...",,,, false)
        ExitApp
    }
}

interceptErrorCheck(actionType){
    global interceptProc, errorImageList

    Delay(1)
    priorIntercept := interceptProc
    interceptProc := true

    matchedError := ""
    For index, needleName in errorImageList {
        if(FindOrLoseImage(needleName, 0, , , true)) {
            matchedError := needleName
            break
        }
    }
    if(matchedError = "")
    {
        interceptProc := priorIntercept
        return false
    }
    if(matchedError = "Common_Error_3ButtonError_Nodata")
    {
        interceptProc := priorIntercept
        return false
    }

    if(matchedError = "Common_Error_Cache")
        adbClick_wbb(137, 430)
    else if(matchedError = "Common_Error_NoResponse" || matchedError = "Common_Error_NoResponseDark")
        adbClick_wbb(46, 299)
    else
        adbClick_wbb(137, 380)
    CreateStatusMessage("An error occurred while processing friends. Restarting.`n(" . failSafeTime . "/45 seconds)")
    Delay(1)
    ReEnterSocial(actionType)
    return true
}

ReEnterSocial(prevAction){
    global interceptProc
    reEnterStart := A_TickCount
    Loop {
        if (DismissFriendFlowBlockingPopup("Re-entering Social"))
            continue

        adbClick_wbb(143, 518)
        reEnterElapsed := (A_TickCount - reEnterStart) // 1000
        if(FindOrLoseImage("Common_ActivatedSocialInMainMenu", 0, reEnterElapsed)) {
            break
        }
        Delay(0.25)
    }

    if(prevAction = "ADD"){
        GoToFriendsList(true, true)
        FindImageAndClick("Friend_SearchFriendWindowCancelButtonCorner", 75, 440)
        FindFriendIDInputAndClick("", "re-enter ADD")
    }
    else if(prevAction = "CLEARALL"){
        GoToFriendsList(false, true)
        FindImageAndClick("Friend_FriendRequestsSubMenu", 167, 467, , 10)
    }
    else if(prevAction = "REMOVE"){
        GoToFriendsList(false, true)
    }
}
;-------------------------------------------------------------------------------
; showcaseLikes
;-------------------------------------------------------------------------------
showcaseLikes() {
    global session

    FindImageAndClick("Friend_CommunityShowcaseMain", 152, 335, , 200)
    session.set("failSafe", A_TickCount)
    failSafeTime := 0

    ; Read the entire file to avoid concurrent access issues
    FileRead, content, %A_ScriptDir%\..\showcase_ids.txt
    ; Remove BOM if present
    if (SubStr(content, 1, 1) = Chr(0xFEFF))
        content := SubStr(content, 2)
    ; Split into lines
    showcaseIDs := StrSplit(content, "`n", "`r")
    ; Trim and filter non-empty
    filteredIDs := []
    for index, line in showcaseIDs {
        trimmed := Trim(line)
        if (trimmed != "")
            filteredIDs.Push(trimmed)
    }

    Loop % filteredIDs.Length()
    {
        showcaseID := filteredIDs[A_Index]
        ; Log for debugging
        LogInfo("Processing showcase ID: " . showcaseID, "ShowcaseLog.txt")
        Delay(2)
        ;TradeTutorialForShowcase()
        ;Delay(2)
        FindImageAndClick("Friend_FriendIDSearchWindow", 224, 467, , 200)
        Delay(2)
        FindImageAndClick("Friend_ShowcaseIDInputFormBlank", 143, 268, , 200)
        Delay(2)
        adbInput(showcaseID)					; Pasting ID
        Delay(2)
        adbClick(200, 364)						; Pressing OK
        Delay(1)
        FindImageAndClick("Friend_CompleteClickShowcaseLike", 160, 195, , 200)
        Delay(4)
        FindImageAndClick("Friend_CommunityShowcaseMain", 138, 500, , 200)
        failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
        CreateStatusMessage("Waiting for Showcase Likes for `n(" . failSafeTime . "/90 seconds)")
    }
}

;-------------------------------------------------------------------------------
; Friend ID input focus - safe click (only inside search dialog)
;-------------------------------------------------------------------------------
IsFriendSearchDialogOpen() {
    return FindOrLoseImage("Friend_SearchFriendWindowCancelButtonCorner", 0, , , true)
}

IsFriendSearchInputReady() {
    return FindOrLoseImage("Friend_FriendIDInputReady", 0, , , true)
        || FindOrLoseImage("Friend_InputFormBlank", 0, , , true)
}

IsFriendProfileDetailsOpen() {
    return FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0, , , true)
        || FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, , , true)
        || FindOrLoseImage("GPTest_NotFavouriteInDetails", 0, , , true)
        || FindOrLoseImage("GPTest_FavouritedInDetails", 0, , , true)
        || FindOrLoseImage("GPTest_FriendRequestButtonInUserDetails", 0, , , true)
        || FindOrLoseImage("Profile_TrophyStandIconInProfile", 0, , , true)
}

RecoverWrongScreenBeforeFriendIDInput(context := "") {
    ctx := (context != "") ? " | " . context : ""

    if (IsFriendSearchInputReady())
        return false

    if (FindOrLoseImage("Friend_AddButtonInFriendList", 0, , , true)
        && !IsFriendSearchDialogOpen()) {
        LogInfo("FriendAdd recovery: on friend list, opening search" . ctx)
        adbClick_wbb(240, 120)
        Delay(1)
        return true
    }

    if (IsFriendSearchDialogOpen())
        return false

    if (IsFriendProfileDetailsOpen())
        LogInfo("FriendAdd recovery: friend profile detected, pressing back" . ctx)
    else
        LogInfo("FriendAdd recovery: not on OK2/search/list, pressing back" . ctx)

    adbClick_wbb(143, 507)
    Delay(0.75)
    return true
}

FindFriendIDInputAndClick(sleepTimeMs := "", context := "") {
    global botConfig, session

    if (sleepTimeMs = "")
        sleepTimeMs := botConfig.get("Delay")

    navTime := 0
    focusTime := 0
    start := A_TickCount
    wasRecovered := false
    ctx := (context != "") ? " | " . context : ""

    Loop {
        if (RecoverWrongScreenBeforeFriendIDInput(context))
            wasRecovered := true

        if (IsFriendSearchInputReady()) {
            if (wasRecovered)
                LogInfo("FriendAdd recovery: OK2 visible again after leaving profile/list" . ctx)
            adbClick_wbb(138, 265)
            Delay(0.25)
            return true
        }

        if (IsFriendSearchDialogOpen()) {
            if (!focusTime || (A_TickCount - focusTime) >= sleepTimeMs) {
                adbClick_wbb(138, 265)
                focusTime := A_TickCount
            }
        } else if (!navTime || (A_TickCount - navTime) >= sleepTimeMs) {
            LogInfo("FriendAdd recovery: still off search dialog, pressing back again" . ctx)
            adbClick_wbb(143, 507)
            Delay(0.75)
            navTime := A_TickCount
            wasRecovered := true
        }

        elapsed := (A_TickCount - start) // 1000
        if (elapsed >= 45) {
            if (IsFriendProfileDetailsOpen() || !IsFriendSearchDialogOpen())
                LogWarn("FriendAdd recovery failed: could not return to OK2 from profile/wrong screen after 45s" . ctx)
            else
                LogWarn("Instance " . session.get("scriptName") . " stuck at OK2 for " . elapsed . "s during friend ID input focus" . ctx)
            restartGameInstance("Stuck at OK2...")
            return false
        }

        Sleep, 100
    }
}

;-------------------------------------------------------------------------------
; EraseInput - Clear friend code input field
;-------------------------------------------------------------------------------
SubmitFriendIDSearch(value, num := 0, total := 0) {
    global session

    Loop, 3 {
        if(num)
            CreateStatusMessage("Entering friend ID " . num . "/" . total . " (" . A_Index . "/3)",,,, false)

        FindFriendIDInputAndClick(1000, "submit index=" . num)
        adbInputEvent("59 122 67")
        Delay(0.25)
        adbInput(value)
        Delay(1)
        adbClick_wbb(187, 365)

        submitStart := A_TickCount
        Loop {
            if(FindOrLoseImage("Friend_RequestButtonInSearchResult", 0, , 80, true)
                || FindOrLoseImage("Friend_WithdrawButton", 0, , , true)
                || FindOrLoseImage("Friend_AcceptedButtonInSearchResult", 0, , , true)
                || FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0, , , true)
                || FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, , , true)
                || FindOrLoseImage("Friend_CannotFriendRequest", 0, , , true)
                || FindOrLoseImage("Common_Error", 0, , , true))
                return true

            if(!FindOrLoseImage("Friend_SearchFriendWindowCancelButtonCorner", 0, , , true))
                return true

            if((A_TickCount - submitStart) > 2000)
                break

            Delay(0.25)
        }

        if(FindOrLoseImage("Friend_RequestButtonInSearchResult", 0, , 80, true)
            || FindOrLoseImage("Friend_WithdrawButton", 0, , , true)
            || FindOrLoseImage("Friend_AcceptedButtonInSearchResult", 0, , , true)
            || FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0, , , true)
            || FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, , , true)
            || FindOrLoseImage("Friend_CannotFriendRequest", 0, , , true)
            || FindOrLoseImage("Common_Error", 0, , , true)
            || !FindOrLoseImage("Friend_SearchFriendWindowCancelButtonCorner", 0, , , true))
            return true

        LogToFile("Friend ID input did not submit; retrying | index=" . num . " | try=" . A_Index)
        if(EraseInput(num, total))
            return true
    }

    LogToFile("Friend ID input failed after retries | index=" . num)
    return false
}

EraseInput(num := 0, total := 0) {
    global session

    if(num)
        CreateStatusMessage("Removing friend ID " . num . "/" . total,,,, false)

    session.set("failSafe", A_TickCount)
    failSafeTime := 0

    Loop {
        if(FindOrLoseImage("Friend_CannotFriendRequest", 0, , , true)
            && IsFriendSearchDialogOpen()) {
            LogToFile("EraseInput clearing cannot-friend-request result | index=" . num)
            adbClick_wbb(138, 265)
            Delay(0.25)
            adbInputEvent("59 122 67")
            if(FindOrLoseImage("Friend_InputFormBlank", 0, failSafeTime, , true))
                return true
            failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
            if(failSafeTime > 10)
                break
            continue
        }

        if(FindOrLoseImage("Friend_RequestButtonInSearchResult", 0, , 80, true)
            || FindOrLoseImage("Friend_WithdrawButton", 0, , , true)
            || FindOrLoseImage("Friend_AcceptedButtonInSearchResult", 0, , , true)
            || FindOrLoseImage("Friend_ReqeustButtonInFriendDetails", 0, , , true)
            || FindOrLoseImage("Friend_AcceptedButtonInFriendDetails", 0, , , true)
            || FindOrLoseImage("Common_Error", 0, , , true)) {
            LogToFile("EraseInput skipped because search result is open | index=" . num)
            return true
        }

        FindFriendIDInputAndClick("", "erase index=" . num)
        adbInputEvent("59 122 67") ; Press Shift + Home + Backspace
        if(FindOrLoseImage("Friend_InputFormBlank", 0, failSafeTime))
            break
        failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
        if(failSafeTime > 10) {
            LogToFile("EraseInput timeout | index=" . num)
            break
        }
    }
    return false
}

CloseFriendDetailsIfOpen(context := "") {
    ctx := (context != "") ? " | " . context : ""
    Loop, 6 {
        if(!IsFriendProfileDetailsOpen())
            return true

        LogInfo("FriendAdd recovery: closing friend profile (" . A_Index . "/6)" . ctx)
        adbClick_wbb(143, 507)
        Delay(0.75)
    }
    LogWarn("FriendAdd recovery: failed to close friend profile" . ctx)
    return false
}

ShouldSkipGenericButtonInSocialWait() {
    reason := ""
    if (FindOrLoseImage("Common_ActivatedHomeInMainMenu", 0, , , true))
        reason := "home active"
    else if (FindOrLoseImage("Friend_BottomDarkHomeIcon", 0, , , true))
        reason := "bottom home"
    else if (FindOrLoseImage("Common_ShopButtonInMain", 0, , , true))
        reason := "home shop"
    else if (FindOrLoseImage("WonderPick_WonderPickButtonInHome", 0, , , true))
        reason := "home wonderpick"
    else if (FindOrLoseImage("Pack_PackPointButton", 0, , , true))
        reason := "pack points"
    else if (FindOrLoseImage("Pack_BackButtonInSelectPackScreen", 0, , , true))
        reason := "pack select"
    else if (FindOrLoseImage("Pack_ReadyForOpenPack", 0, , , true))
        reason := "ready open pack"
    else if (FindOrLoseImage("Pack_HourglassImageAfterOpenPackClick", 0, , , true))
        reason := "hourglass pack"
    else if (FindOrLoseImage("Pack_HourglassAndPokeGoldImageAfterOpenPackClick", 0, , , true))
        reason := "hourglass/gold pack"
    else if (FindOrLoseImage("Pack_PokeGoldImageAfterOpenPackClick", 0, , , true))
        reason := "gold pack"

    return (reason != "")
}

DismissFriendFlowBlockingPopup(context := "") {
    if (!FindOrLoseImage("Common_PopupXButtonInMain", 0, , , true))
        return false

    LogDebug("Dismissed friend flow blocking popup | context=" . context)

    if (context != "")
        CreateStatusMessage("Closing popup during friend flow`n" . context,,,, false)
    else
        CreateStatusMessage("Closing popup during friend flow",,,, false)

    adbClick_wbb(137, 480)
    Delay(1)
    return true
}

IsSocialHubReadyForFriends() {
    return FindOrLoseImage("Friend_SocialHubFriendButton", 0, , 20, true)
}

IsSocialTabActiveOnHub(failSafeTime := 0) {
    return FindOrLoseImage("Common_ActivatedSocialInMainMenu", 0, failSafeTime)
        && IsSocialHubReadyForFriends()
}

ReturnToSocialHubIfNeeded() {
    if (IsSocialHubReadyForFriends())
        return

    adbClick_wbb(143, 518)
    waitStart := A_TickCount
    Loop {
        if (IsSocialHubReadyForFriends())
            return
        if ((A_TickCount - waitStart) // 1000 >= 10)
            return
        Delay(0.5)
    }
}

GoToFriendsList(isKeepSearch := false, skipTutorialProc := false) {
    global session

    session.set("failSafe", A_TickCount)
    failSafeTime := 0
    mainLoopBreak := false
    Loop {
        if (DismissFriendFlowBlockingPopup("Goto friends screen"))
            continue

        if(FindOrLoseImage("Common_ActivatedSocialInMainMenu", 0, failSafeTime, , true)) {
            if(FindOrLoseImage("Common_ColorChangeButton2", 0, , 80)) {
                adbClick_wbb(200, 80)
            }
            if(!IsSocialHubReadyForFriends()) {
                failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
                Delay(0.5)
                CreateStatusMessage("Waiting for Social hub`n(" . failSafeTime . "/45 seconds)")
                continue
            }

            ; If main screen(social): Click friends button
            adbClick_wbb(38, 460)
        }
        else if(FindOrLoseImage("Friend_AddButtonInFriendList", 0, failSafeTime, , true)) {
            ; If friends list screen: Click Search button
            adbClick_wbb(240, 120)
            Delay(1)
        }
        else if(FindOrLoseImage("Friend_SearchFriendButton", 0, failSafeTime, , true)) {
            if(!isKeepSearch){
                Loop {
                    if (DismissFriendFlowBlockingPopup("Leaving friend search"))
                        continue

                    if(FindOrLoseImage("Friend_SearchFriendButton", 0, failSafeTime, , true)) {
                        adbInputEvent("111") ;send ESC
                    }
                    else if(FindOrLoseImage("Friend_AddButtonInFriendList", 0, failSafeTime)) {
                        mainLoopBreak := true
                        break
                    }
                    Delay(2)
                    failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
                    if (failSafeTime > 45) {
                        restartGameInstance("Stuck at Goto friends screen")
                        return
                    }
                }
            }
            else
                break

            if(mainLoopBreak)
                break
        }
        else{
            if(!skipTutorialProc) {
                if(TryHandleTradeTutorial(failSafeTime))
                    continue
                else if(!TryDismissSocialFirstTutorial(failSafeTime))
                    adbClick_wbb(155, 425)
            }
        }
        Delay(0.25)
        failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
        CreateStatusMessage("Goto friends screen`n(" . failSafeTime . "/45 seconds)")
        if (failSafeTime > 45) {
            restartGameInstance("Stuck at Goto friends screen")
            return
        }
    }
}

;-------------------------------------------------------------------------------
; getFriendCode - Get friend code from current account
;-------------------------------------------------------------------------------
getFriendCode(alreadyAtHome := false) {
    prof := Prof_Scope(A_ThisFunc)
    global session

    CreateStatusMessage("Getting friend code...",,,, false)
    if (!alreadyAtHome) {
        Sleep, 2000
        session.set("failSafe", A_TickCount)
        failSafeTime := 0
        Loop {
            Delay(1)
            if(FindOrLoseImage("Pack_SkipButtonAfterOpenPack", 0, failSafeTime)) {
                adbClick_wbb(239, 497)
            } else if(FindOrLoseImage("Pack_NextButtonAfterOpenPack", 0, failSafeTime)) {
                adbClick_wbb(146, 494) ;146, 494
            } else if(FindOrLoseImage("Next2", 0, failSafeTime)) {
                adbClick_wbb(146, 494) ;146, 494
            } else if(FindOrLoseImage("Pack_BackButtonInSelectPackScreen", 0, failSafeTime)) {
                break
            } else if(FindOrLoseImage("Friend_BottomDarkHomeIcon", 0, failSafeTime)) {
                break
            } else {
                adbclick_wbb(146, 494)
            }
            failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
            CreateStatusMessage("Waiting for Home`n(" . failSafeTime . "/45 seconds)")
            if(failSafeTime > 45)
                restartGameInstance("Stuck at Home")
        }
    }
    session.set("friendCode", AddFriends(false, true))

    return session.get("friendCode")
}

AccountFriendInfo_CopyFriendCodeFromCurrentScreen() {
    global session

    CreateStatusMessage("Retrieving account info`nCopying Friend Code...",,,, false)
    Delay(3)

    friendCode := ""
    copyStartX := 214
    copyY := 202
    copyStepX := 2
    maxCopyAttempts := 10

    ; Try the copy button area from left to right until clipboard yields a valid code.
    Loop, %maxCopyAttempts% {
        CreateStatusMessage("Retrieving account info`nCopying Friend Code (" . A_Index . "/" . maxCopyAttempts . ")")
        clickX := copyStartX + ((A_Index - 1) * copyStepX)

        hLock := AccountFriendInfo_AcquireClipboardLock(800)
        if (!hLock) {
            Sleep, 40
            continue
        }

        Clipboard := ""
        adbClick_wbb(clickX, copyY)
        ClipWait, 0.5
        copiedValue := RegExReplace(Clipboard, "\D", "")
        Clipboard := ""
        AccountFriendInfo_ReleaseClipboardLock(hLock)

        if (RegExMatch(copiedValue, "^\d{16}$")) {
            friendCode := copiedValue
            break
        }
        Sleep, 120
    }
    Delay(1)

    session.set("friendCode", friendCode)
    if (friendCode != "")
        CreateStatusMessage("Retrieving account info`nFriend Code copied",,,, false)
    return session.get("friendCode")
}

;-------------------------------------------------------------------------------
; EnsureAccountFriendInfo - Persist own account name + friend code once per account
;-------------------------------------------------------------------------------
EnsureAccountFriendInfo(methodType := "", alreadyOnFriendSearch := false, force := false) {
    prof := Prof_Scope(A_ThisFunc)
    global botConfig, session, DeadCheck

    if (!force && !botConfig.get("saveAccountFriendInfo"))
        return false
    if (DeadCheck = 1)
        return false
    if (methodType = "")
        methodType := botConfig.get("deleteMethod")
    if (methodType = "Create Bots (13P)" && !force)
        return false
    if (!force && (!session.get("injectMethod") || !session.get("loadedAccount")))
        return false

    accountFileName := session.get("accountFileName")
    if (accountFileName = "")
        return false

    deviceAccount := AccountFriendInfo_GetDeviceAccount()
    if (deviceAccount = "")
        return false

    if (session.get("accountFriendInfoChecked") = deviceAccount)
        return false

    CreateStatusMessage("Retrieving account info`nChecking saved metadata...",,,, false)

    accountSourcePath := session.get("loadedAccount")
    if (accountSourcePath = "" && methodType = "Create Bots (13P)")
        accountSourcePath := A_ScriptDir . "\..\Accounts\Saved\" . session.get("scriptName") . "\" . accountFileName

    accountMeta := AccountMetadata_Get(session.get("scriptName"), accountFileName, accountSourcePath)
    existingName := Trim(accountMeta["accountName"])
    existingFriendCode := RegExReplace(accountMeta["friendCode"], "\D", "")
    if (existingName != "" && existingName != "Unknown" && RegExMatch(existingFriendCode, "^\d{16}$")) {
        session.set("accountName", existingName)
        session.set("friendCode", existingFriendCode)
        session.set("accountFriendInfoChecked", deviceAccount)
        return true
    }

    CreateStatusMessage("Retrieving account info`nMissing name or Friend Code",,,, false)

    if (alreadyOnFriendSearch)
        friendCode := AccountFriendInfo_CopyFriendCodeFromCurrentScreen()
    else {
        CreateStatusMessage("Retrieving account info`nGoing to Friends profile...",,,, false)
        friendCode := AddFriends(false, true)
    }

    cleanFriendCode := RegExReplace(friendCode, "\D", "")
    if (!RegExMatch(cleanFriendCode, "^\d{16}$"))
        cleanFriendCode := existingFriendCode

    accountName := AccountFriendInfo_ReadNameFromFriendProfile()
    if (accountName = "" || accountName = "Unknown")
        accountName := existingName

    if (!alreadyOnFriendSearch && methodType != "Create Bots (13P)")
        AccountFriendInfo_ReturnToMain()

    if ((accountName = "" || accountName = "Unknown") && !RegExMatch(cleanFriendCode, "^\d{16}$")) {
        LogWarn("Could not retrieve account friend info for " . accountFileName)
        return false
    }

    accountMeta["deviceAccount"] := deviceAccount
    if (accountName != "" && accountName != "Unknown")
        accountMeta["accountName"] := accountName
    if (RegExMatch(cleanFriendCode, "^\d{16}$"))
        accountMeta["friendCode"] := cleanFriendCode

    CreateStatusMessage("Retrieving account info`nSaving to account JSON...",,,, false)
    saved := AccountMetadata_SaveAccount(session.get("scriptName"), accountFileName, accountMeta)
    if (saved) {
        session.set("accountName", accountMeta["accountName"])
        session.set("friendCode", accountMeta["friendCode"])
        session.set("accountFriendInfoChecked", deviceAccount)
        CreateStatusMessage("Retrieving account info`nSaved name and Friend Code",,,, false)
        LogInfo("Saved account friend info for " . accountFileName)
    } else {
        CreateStatusMessage("Retrieving account info`nSave failed",,,, false)
        LogWarn("Failed to save account friend info for " . accountFileName)
    }
    return saved
}

AccountFriendInfo_GetDeviceAccount() {
    global session

    deviceAccount := session.get("deviceAccount")
    if (deviceAccount != "")
        return deviceAccount

    deviceAccount := GetDeviceAccountFromXML()

    if (deviceAccount != "")
        session.set("deviceAccount", deviceAccount)
    return deviceAccount
}

AccountFriendInfo_ReadNameFromFriendProfile(statusPrefix := "Retrieving account info", initialSleepMs := 5000) {
    global session

    CreateStatusMessage(statusPrefix . "`nReading account name...",,,, false)
    if (initialSleepMs > 0)
        Sleep, %initialSleepMs%

    tempDir := A_ScriptDir . "\..\Screenshots\temp"
    if !FileExist(tempDir)
        FileCreateDir, %tempDir%

    usernameScreenshotFile := tempDir . "\" . session.get("scriptName") . "_Username.png"
    CreateStatusMessage(statusPrefix . "`nTaking name screenshot...",,,, false)
    adbTakeScreenshot(usernameScreenshotFile)
    Sleep, 100

    username := ""
    try {
        if (IsFunc("ocr")) {
            CreateStatusMessage(statusPrefix . "`nRunning OCR on name...",,,, false)
            playerName := ""
            allowedUsernameChars := "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789-+_"
            usernamePattern := "[\w-_]+"

            if (RefinedOCRText(usernameScreenshotFile, 145, 235, 250, 35, allowedUsernameChars, usernamePattern, playerName))
                username := playerName
        }
    } catch e {
        LogWarn("Failed to OCR account name: " . e.message, "OCR.txt")
    }

    if (FileExist(usernameScreenshotFile))
        FileDelete, %usernameScreenshotFile%

    return Trim(username)
}

AccountFriendInfo_ReturnToMain() {
    global session

    CreateStatusMessage("Retrieving account info`nReturning Home...",,,, false)
    session.set("failSafe", A_TickCount)
    failSafeTime := 0
    Loop, {
        adbClick_wbb(143, 518)
        Delay(3)
        if(FindOrLoseImage("Common_ActivatedSocialInMainMenu", 0, failSafeTime))
            break
        else if(FindOrLoseImage("Friend_SearchFriendWindowCancelButtonCorner", 0, failSafeTime))
            adbClick_wbb(80, 365)
        failSafeTime := (A_TickCount - session.get("failSafe")) // 1000
        CreateStatusMessage("Retrieving account info`nReturning to Social (" . failSafeTime . "/45 seconds)")
    }

    Loop {
        if(FindOrLoseImage("Friend_BottomDarkHomeIcon", 0))
            break
        else {
            CreateStatusMessage("Retrieving account info`nOpening Home from Social...",,,, false)
            adbClick_wbb(40, 516)
            Delay(0.1)
            adbClick_wbb(175, 445)
            DelayH(500)
        }
    }
    session.set("accountFriendInfoReturnedHome", true)
}
